{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": 130,
   "metadata": {},
   "outputs": [],
   "source": [
    "using SimJulia"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "#### How to chose if only one event needs to be realised\n",
    "##### Example setting: \n",
    "an agent requests a resource but only has a limited amount of patience before no longer wanting/needing the resource. \n",
    "```Julia\n",
    "@resumable function agent(sim::Simulation,con::Resource)\n",
    "    req = request(con)\n",
    "    res = @yield req | timeout(sim,4)\n",
    "    if res[req].state == SimJulia.processed\n",
    "        println(\"using my resource...\")\n",
    "        @yield timeout(sim,1)\n",
    "        release(res)\n",
    "    else\n",
    "        println(\"patience ran out... on time $(sim.time)\")\n",
    "        cancel(con,req)\n",
    "    end\n",
    "end\n",
    "\n",
    "sim = Simulation()\n",
    "con = Resource(sim,0)\n",
    "@process agent(sim,con)\n",
    "run(sim,10)\n",
    "```\n",
    "`patience ran out... on time 4.0`\n",
    "\n",
    "For the example, a simulation is made with a `::Resource` with a capacity of $0$. So the agent can never obtain the requested resource. In the `agent` function the following happens:\n",
    "1. A request for `con::Resource` is made. The type of `req` is `SimJulia.Put`. This event will be triggered by an `@yield`\n",
    "2. the variable `req` is a dictionary with the events as key and the `::StateValue` as value. The first event to have been processed will have its `::StateValue` equal to `SimJulia.processed`\n",
    "3. the `if` conditions test whether the `::StateValue` of our request is equal to `SimJulia.processed`. \n",
    "  1. If this is the case, the agent obtains the `::Resource`, uses it for 1 time unit and releases it back for further use.\n",
    "  2. If this is NOT the case, the other event will have taken place (in this case the timeout) and we remove the request from the `::Resource` queue with `cancel`.\n",
    "4. the simulation terminates since no more processes are active on time 4.0.\n",
    "\n",
    "```\n",
    "req: SimJulia.Put 4\n",
    "\n",
    "res: Dict{AbstractEvent,SimJulia.StateValue}(SimJulia.Timeout 5=>StateValue(processed, nothing),\n",
    "SimJulia.Put 4=>StateValue(idle, nothing))```\n",
    "\n",
    "The above method can also be used for more than two events where the completion of a single one of them is sufficient."
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "#### How to chose between whatever resource comes available first\n",
    "Setting: when dealing with multiple available resources, it might be sufficient for an agent to obtain a single one of them in order to be able to proceed. In the example below this is illustrated for two different resources. The difference between both is the time required before they become available again. One of both resource has two units while the other only has one. Several agents (requiring just one of both resources to function) are active.\n",
    "```Julia\n",
    "@resumable function agent(sim::Simulation,res1::Resource,res2::Resource,name::String)\n",
    "    r1 = request(res1)\n",
    "    r2 = request(res2)\n",
    "    res = @yield r1 | r2\n",
    "    if res[r1].state == SimJulia.processed\n",
    "        println(\"$name obtains slow resource on time $(sim.time)\")\n",
    "        if res[r2].state == SimJulia.processed\n",
    "            release(res2)\n",
    "        else\n",
    "            cancel(res2,r2)\n",
    "        end\n",
    "        @yield timeout(sim,2)\n",
    "        release(res1)\n",
    "        println(\"$name releases slow resource on time $(sim.time)\")\n",
    "    else\n",
    "        cancel(res1,r1)\n",
    "        println(\"$name obtains fast resource on time $(sim.time)\")\n",
    "        @yield timeout(sim,1)\n",
    "        release(res2)\n",
    "        println(\"$name releases fast resource on time $(sim.time)\")\n",
    "    end\n",
    "end\n",
    "\n",
    "sim = Simulation()\n",
    "slowresource = Resource(sim,1)\n",
    "fastresource = Resource(sim,2)\n",
    "for i in 1:7\n",
    "    @process agent(sim,slowresource,fastresource,\"Agent $i\")\n",
    "end\n",
    "run(sim)\n",
    "```\n",
    "\n",
    "```\n",
    "Agent 1 obtains slow resource on time 0.0\n",
    "Agent 2 obtains fast resource on time 0.0\n",
    "Agent 3 obtains fast resource on time 0.0\n",
    "Agent 2 releases fast resource on time 1.0\n",
    "Agent 3 releases fast resource on time 1.0\n",
    "Agent 4 obtains fast resource on time 1.0\n",
    "Agent 5 obtains fast resource on time 1.0\n",
    "Agent 1 releases slow resource on time 2.0\n",
    "Agent 4 releases fast resource on time 2.0\n",
    "Agent 5 releases fast resource on time 2.0\n",
    "Agent 6 obtains slow resource on time 2.0\n",
    "Agent 7 obtains fast resource on time 2.0\n",
    "Agent 7 releases fast resource on time 3.0\n",
    "Agent 6 releases slow resource on time 4.0\n",
    "```\n",
    "In the agent the following happens:\n",
    "1. Individual requests for both resources `res1` and `res2` are made. The type of `r1`/`r2` is `SimJulia.Put`. The events will be triggered by an `@yield`\n",
    "2. the variable `res` is a dictionary with the events as key and the `::StateValue` as value. The first event to have been processed will have its `::StateValue` equal to `SimJulia.processed`\n",
    "3. the `if` conditions test whether the `::StateValue` of our request for resource `res1` is equal to `SimJulia.processed`. \n",
    "  1. If this is the case, the agent obtains the `::Resource`. Meanwhile is it possible that `res2` was also available at the same time. In order to avoid double resource usage we need to do one of both things:\n",
    "    * release `res2` if it was obtained at the sime time as `res1`\n",
    "    * remove the request for `res2` from its queue with `cancel`\n",
    "  \n",
    "    In the example `res1` is consided as the slow resource. The agent uses it for 2 time units and releases it back for further use.\n",
    "  2. If this is NOT the case, we will have obtained `res2`. Again, to avoid double resource usage, we remove the request for `res1` from its queue with `cancel`. The agent uses it for 1 time unit and releases `res2` back for further use.\n",
    "4. the simulation terminates when all agents have been processed."
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "#### Competing for a Resource\n",
    "When to entities request a `::Resource` at the same time, you can determine which one should get priority base on the `priority` keyword in the `request` instruction. The lower the integer value, the higher the priority. Default values for all request are equal to 0.\n",
    "\n",
    "```Julia\n",
    "using SimJulia\n",
    "\n",
    "@resumable function grabsource(sim::Simulation,res::Resource)\n",
    "    for i in 1:4\n",
    "        @yield request(res)\n",
    "    end\n",
    "    println(\"got them all on time $(sim.time)\")\n",
    "    @yield timeout(sim,10)\n",
    "    @yield release(res)\n",
    "    println(\"released ONE on time $(sim.time)\")\n",
    "end\n",
    "\n",
    "@resumable function priosource(sim::Simulation,res::Resource)\n",
    "    @yield timeout(sim,1)\n",
    "    @yield request(res,priority = -1)\n",
    "    println(\"PRIO got it at $(sim.time)\")\n",
    "    @yield timeout(sim,1)\n",
    "    @yield release(res,priority = -1)\n",
    "end\n",
    "\n",
    "@resumable function secondsource(sim::Simulation,res::Resource)\n",
    "    @yield timeout(sim,1)\n",
    "    @yield request(res,priority = 1)\n",
    "    println(\"SECOND got it at $(sim.time)\")\n",
    "    @yield timeout(sim,1)\n",
    "    @yield release(res)\n",
    "end\n",
    "\n",
    "sim = Simulation()\n",
    "res = Resource(sim,4)\n",
    "@process grabsource(sim, res)\n",
    "@process priosource(sim, res)\n",
    "@process secondsource(sim,res)\n",
    "run(sim,20)\n",
    "```\n",
    "```\n",
    "got them all on time 0.0\n",
    "released ONE on time 10.0\n",
    "PRIO got it at 10.0\n",
    "SECOND got it at 11.0\n",
    "```\n",
    "\n",
    "1. Initialy (in time 0) all `Resources` are attributed to the grabsource process.\n",
    "2. On time one, both the priosource and secondsource process request a resource. The priority of priosource however is a lower number and gets a higher priority.\n",
    "2. On time 10 the grabsource process releases one `Resources`. At the sime time, the `Resource`is attributed to the priosource process.\n",
    "\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": []
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.0.0",
   "language": "julia",
   "name": "julia-1.0"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.0.0"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 2
}
